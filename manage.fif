#!/usr/bin/env fift -s
"TonUtil.fif" include
"Asm.fif" include

{ ."Manual TON DNS management script." cr cr
  ."usage: " $0 type ." <filename-base> action arg1 arg2 ... argN" cr
  ."  touch <workchain-id>: create new or show existing install" cr
  ."  set <domain> <seqno> <category_id> <address>" cr
  ."  upgrade <seqno>" cr
  ."  tranasfer <seqno> <new-public-key>" cr
  1 halt
} : usage

// (msg -- Msg)
{
    dup hashu @' mtdns-privkey ed25519_sign_uint // msg sig
    <b b{1000100} s, @' addr addr, 0 Gram, b{00} s, swap B, swap ref, b>
    2 boc+>B "deployments/last-query.boc" tuck B>file
    ."Saved query to file " type cr
} : create_ext_msg

// (StateInit msg -- InitMsg)
{
    dup hashu @' mtdns-privkey ed25519_sign_uint // StateInit msg sig
    rot // msg sig StateInit
    <b b{1000100} s, @' addr addr, b{000010} s, swap <s s, b{0} s, swap B, swap ref, b>
    2 boc+>B "deployments/last-query.boc" tuck B>file
    ."Saved query to file " type cr
} : create_init_msg

// (wc addr -- )
{
    ."Contract address = " 2dup .addr cr
    ."Non-bounceable address (for init): " 2dup 7 .Addr cr
    ."Bounceable address (for later access): " 6 .Addr cr
} : print_addr

"deployments/" $1 $+ =: file-base

$2 "show" $=
{
    @' file-base +".addr" load-address print_addr
    quit
} if

$2 "touch" $=
{
    $3 parse-workchain-id =: wc

    ."Initialising dns contract '" @' file-base type ."' in workchain " @' wc . cr

    @' file-base +".pk" load-generate-keypair =: mtdns-privkey =: mtdns-pubkey

    // create StateInit
    "mtdns.fif" include
    <b 0 32 u, @' mtdns-pubkey B, null dict, b>
    null
    <b b{0011} s, 3 roll ref, rot ref, swap dict, b>  // StateInit

    // calculate and save addres
    dup hashu @' wc swap 2=: addr
    @' addr 2dup @' file-base +".addr"  save-address print_addr

    <b 0 32 u, 0x0 32 u, b> // StateInit InitMsg
    create_init_msg

    quit
} if

$2 "set" $=
{
    // <domain> <seqno> <category_id> <address>
    $3 =: domain
    $4 parse-int =: seqno
    $5 parse-int =: category_id
    $6 true parse-load-address drop 2=: dest_addr

    @' file-base +".addr" load-address 2=: addr
    @' file-base +".pk" load-keypair  =: mtdns-privkey =: mtdns-pubkey

    @' domain <b swap $, b> // domain name in separete cell
    <b @' seqno 32 u, 0x2 32 u, @' category_id 16 i, @' dest_addr addr, swap ref, b> // msg
    create_ext_msg

    quit
} if

$1 "upgrade" $=
{
    ."init!" cr
} if

$1 "transfer" $=
{
    ."init!" cr
} if

usage
